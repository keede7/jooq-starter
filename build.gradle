plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'nu.studer.jooq' version '8.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('jooqVersion', '3.18.4')
    set('mysqlVersion', '8.0.29')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    runtimeOnly "mysql:mysql-connector-java:$mysqlVersion"

    // JOOQ
    implementation "org.jooq:jooq:$jooqVersion"
    runtimeOnly "org.jooq:jooq-meta:$jooqVersion"
    runtimeOnly "org.jooq:jooq-codegen:$jooqVersion"

    jooqGenerator "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
    jooqGenerator "mysql:mysql-connector-java:$mysqlVersion"
}

// https://github.com/etiennestuder/gradle-jooq-plugin/blob/main/example/use_groovy_dsl/build.gradle
jooq {
    version = '3.18.4'
//    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // the default (can be omitted)

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                // DataBase로 부터 Code를 생성하기 때문에? 미리 들어가 있어야 한다?
                // 그렇기 때문에 H2로 설정을 하게 되면 스크립트를 읽어서라도 코드를 생성하게 만들어야 한다.
                // 현재 설정으로는 파일을 찾지 못하고 있어서 진행이 불가능하다.
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3306/jooqdb?useSSL=false&serverTimezone=Asia/Seoul&characterEncoding=UTF-8&allowPublicKeyRetrieval=true'
                    user = 'root'
                    password = 'root'
                    properties {
                        property {
                            key = 'PAGE_SIZE'
                            value = 2048
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                    }
                    generate {
                        deprecated = false
                        records = false
                        immutablePojos = false
                        fluentSetters = false
                    }
                    target {
                        packageName = 'nu.studer.sample'
                        // 해당 이름을 가진 디렉토리 하위로 packageName을 가진 폴더를 생성하고
                        // 그 폴더 안에 DB관련 폴더들이 추가된다.
//                        directory = 'src/generated/jooq'
                        directory = 'build/generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
